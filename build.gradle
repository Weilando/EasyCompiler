plugins {
    id 'java'
}

group 'org.example'
version '1.0.0'

sourceCompatibility = 1.17

repositories {
    mavenCentral()
}

dependencies {
    // https://mvnrepository.com/artifact/sablecc/sablecc
    implementation group: 'sablecc', name: 'sablecc', version: '3.2-1'
    // https://mvnrepository.com/artifact/commons-cli/commons-cli
    implementation group: 'commons-cli', name: 'commons-cli', version: '1.6.0'

    testImplementation("org.junit.jupiter:junit-jupiter-api:5.8.1")
    testRuntimeOnly("org.junit.jupiter:junit-jupiter-engine:5.8.1")
}

test {
    useJUnitPlatform()
    testLogging {
        events "failed"
        exceptionFormat "full"
    }
}

task getSableCC(type: Copy) {
    mkdir 'libs/'
    from configurations.runtimeClasspath
    into 'libs/'
}

task runSableCC(type: Exec) {
    inputs.file('src/main/java/EasyCompiler.scc').withPathSensitivity(PathSensitivity.RELATIVE)
    outputs.dirs(['src/main/java/analysis', 'src/main/java/lexer', 'src/main/java/node', 'src/main/java/parser'])

    workingDir = "src/main/java"
    commandLine 'java'
    args = ['-jar', '../../../libs/sablecc-3.2-1.jar', 'EasyCompiler.scc']
}

task copyExpressionNode(type: Copy) {
    inputs.file('src/main/resources/PExpr.java')
    outputs.file('src/main/java/node/PExpr.java')

    from 'src/main/resources/PExpr.java'
    into 'src/main/java/node/'
}

task copyLexerDatToOut(type: Copy) {
    inputs.file('src/main/java/lexer/lexer.dat')withPathSensitivity(PathSensitivity.RELATIVE)
    outputs.file('out/production/classes/lexer/lexer.dat')

    from 'src/main/java/lexer/lexer.dat'
    into 'out/production/classes/lexer'
}

task copyParserDatToOut(type: Copy) {
    inputs.file('src/main/java/parser/parser.dat')withPathSensitivity(PathSensitivity.RELATIVE)
    outputs.file('out/production/classes/parser/parser.dat')

    from 'src/main/java/parser/parser.dat'
    into 'out/production/classes/parser'
}

task copyLexerDatToBuild(type: Copy) {
    inputs.file('src/main/java/parser/parser.dat')withPathSensitivity(PathSensitivity.RELATIVE)
    outputs.file('build/classes/java/main/lexer/lexer.dat')

    from 'src/main/java/lexer/lexer.dat'
    into 'build/classes/java/main/lexer'
}

task copyParserDatToBuild(type: Copy) {
    inputs.file('src/main/java/parser/parser.dat')withPathSensitivity(PathSensitivity.RELATIVE)
    outputs.file('build/classes/java/main/parser/parser.dat')

    from 'src/main/java/parser/parser.dat'
    into 'build/classes/java/main/parser'
}

jar {
    manifest {
        attributes(
            "Main-Class": "EasyCompiler"
        )
    }
    from {
        configurations.runtimeClasspath.collect { it.isDirectory() ? it : zipTree(it) }
    }
    archiveFileName.set("EasyCompiler.jar")
}

runSableCC.dependsOn(getSableCC)
copyExpressionNode.dependsOn(runSableCC)
compileJava.dependsOn(runSableCC, copyExpressionNode)
compileTestJava.dependsOn(runSableCC)
test.dependsOn(copyLexerDatToBuild, copyParserDatToBuild)
copyLexerDatToOut.dependsOn(runSableCC, compileJava, compileTestJava)
copyParserDatToOut.dependsOn(runSableCC, compileJava, compileTestJava)
copyLexerDatToBuild.dependsOn(copyLexerDatToOut)
copyParserDatToBuild.dependsOn(copyParserDatToOut)
jar.dependsOn(copyLexerDatToBuild, copyParserDatToBuild)